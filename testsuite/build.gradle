import java.util.concurrent.TimeUnit

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

subprojects {
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs = ['src/java']
        test.java.srcDirs = ['test/java']
    }
}

sourceSets.test.java.srcDirs = ["java"]

dependencies {
    implementation "org.msgpack:jackson-dataformat-msgpack:0.8.16"
    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation project(':sdk')
    implementation project(':api')
    implementation project(':rt')
    implementation project(':tooling')
    implementation project(':samples')
    testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    testImplementation files(project(':rt').sourceSets.test.output)
}

compileTestJava {
    options.warnings = true
    options.deprecation = true
}

class ChainTest extends Test {
    String configFile = "./data/config.json"
    String logFile = ".chain/normal.log"
    String envFile = "./data/env.properties"
    String chainDir = ".chain/normal"
    String gsFile = null
    String gsDir = null
    String dockerEnv = null
    Process process = null

    void startServer() {
        List<String> commands = new LinkedList<String>()
        commands.add("../bin/gochain")
        commands.add("--config")
        commands.add(configFile)
        commands.add("--chain_dir")
        commands.add(chainDir)
        if (gsFile != null) {
            commands.add("--genesis_storage")
            commands.add(gsFile)
        } else if (gsDir != null) {
            commands.add("--genesis")
            commands.add(gsDir)
        }
        def logLevel = System.getProperty("LOG_LEVEL")
        if (logLevel != null && logLevel.length() != 0) {
            commands.add("--log_level=" + logLevel)
        }

        Runtime.getRuntime().exec("rm -rf " + chainDir)

        ProcessBuilder pb = new ProcessBuilder(commands)

        // set working directory
        pb.directory(new File("."))

        File log_file = new File(logFile)
        pb.redirectError(log_file)
        pb.redirectOutput(log_file)

        process = pb.start()

        // Hook shutdown for kill the process for sure
        Runtime.getRuntime().addShutdownHook(new Thread(new Runnable() {
            void run() {
                stopServer()
            }
        }))
    }

    void startDocker(String serverPort) {
        List<String> commands = new LinkedList<String>()
        commands.add("docker")
        commands.add("run")
        commands.add("-dit")
        commands.add("-v")
        commands.add(getWorkingDir().toString() + ":" + "/testsuite")
        commands.add("-p")
        commands.add("${serverPort}:${serverPort}")
        commands.add("--env-file")
        commands.add("data/dockerenv/" + dockerEnv)
        commands.add("--name")
        commands.add("gochain-" + dockerEnv)
        commands.add("goloop/gochain")
        process = commands.execute()
        process.waitFor(1, TimeUnit.SECONDS)
    }

    @TaskAction
    @Override
    void executeTests() {
        def noServer = System.getProperty("NO_SERVER")
        if (noServer == null || noServer != "true") {
            def useDocker = System.getProperty("USE_DOCKER", "false")
            logging.println("STARTING Stand-alone server")
            logging.println("Docker: ${useDocker}")
            logging.println("Config: ${configFile}")
            logging.println("Log   : ${logFile}")
            logging.println("Env   : ${envFile}")
            if (gsFile != null) {
                logging.println("GSFile: ${gsFile}")
            }
            if (useDocker != null && useDocker == "true") {
                startDocker(getServerPort())
            } else {
                startServer()
            }
            systemProperty("CHAIN_ENV", envFile)

            // wait for a while to get ready.
            sleep(1000)
        } else {
            systemProperty("CHAIN_ENV", System.getProperty("CHAIN_ENV"))
        }
        try {
            super.executeTests()
        } finally {
            stopServer()
            stopDocker()
        }
    }

    private String getServerPort() {
        def stdout = new ByteArrayOutputStream()
        def proc = ['sh', '-c', "grep localhost ${envFile} | cut -d \':\' -f 3"].execute()
        proc.waitForProcessOutput(stdout, System.err)
        def serverPort = stdout.toString().trim()
        logging.println("ServerPort: ${serverPort}")
        return serverPort
    }

    synchronized void stopServer() {
        if (process != null && process.isAlive()) {
            logging.println("STOPPING Stand-alone server")
            process.destroy()
            process.waitFor(1, TimeUnit.SECONDS)
            process = null
        }
    }

    synchronized void stopDocker() {
        if (process != null && process.exitValue() == 0) {
            logging.println("STOPPING docker container")
            ["docker", "stop", "gochain-${dockerEnv}"].execute().waitFor()
            ["docker", "rm", "gochain-${dockerEnv}"].execute().waitFor()
            process = null
        }
    }
}

task testJavaScore(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "javaScore"
    }
    configFile = "./data/config/java.json"
    chainDir = ".chain/javascore"
    logFile = ".chain/javascore.log"
    envFile = "./data/chainenv/java.props"
    dockerEnv = "javascore"

    dependsOn 'cleanTestJavaScore'
    testLogging.showStandardStreams = true
}

task testJavaGov(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "javaGov"
    }
    configFile = "./data/config/javagov.json"
    chainDir = ".chain/javagov"
    logFile = ".chain/javagov.log"
    gsDir = "./data/genesisStorage/genesis_javagov.json"
    envFile = "./data/chainenv/javagov.props"
    dockerEnv = "javagov"

    dependsOn 'cleanTestJavaGov'
    testLogging.showStandardStreams = true
}

task testHavah(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "havah"
    }
    configFile = "./data/config/javagov.json"
    chainDir = ".chain/javagov"
    logFile = ".chain/javagov.log"
    gsDir = "./data/genesisStorage/genesis_havah.json"
    envFile = "./data/chainenv/javagov.props"
    dockerEnv = "javagov"

    dependsOn 'cleanTestJavaGov'
    testLogging.showStandardStreams = true
}

task test(overwrite: true) {
    dependsOn 'testJavaScore', 'testJavaGov', 'testHavah'
}
